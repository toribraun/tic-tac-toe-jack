class Board {
    field Array boardArray;
    field int boardX, boardY, boardSize, boardPixelSize, lineThickness, cellPixelSize;
    field int totalCellCount, freeCellCount;
    field int signSize;

    constructor Board new(int size) {		
        let boardX = 200;
        let boardY = 60;
		let boardSize = size;
		let boardPixelSize = 120;
		let lineThickness = 2;
		let cellPixelSize = boardPixelSize/size;
        let signSize = cellPixelSize / 2;
        let totalCellCount = size*size;
		let boardArray = Array.new(totalCellCount);   // 0 - empty, 1 - cross, 2 - zer
        do clearBoard();
		return this;
	}

    method void dispose() {
		do Screen.setColor(false);
		do Screen.drawRectangle(boardX, boardY, boardX+boardPixelSize, boardY+boardPixelSize);
		do Screen.setColor(true);
		do Memory.deAlloc(boardArray);
		return;
	}
	
	method void clearBoard() {
		var int i;
		do Screen.clearScreen();
		let freeCellCount = totalCellCount;
		while(i<totalCellCount) {
			let boardArray[i] = 0;
			let i = i + 1;
		}
		return;
	}

	method void drawBoard() {
		var int offset;
        var int i;
		let offset = lineThickness/2;
        let i = 1;

        while(i<boardSize) {
            // draw vertical line
		    do Screen.drawRectangle(boardX+(i*cellPixelSize)-offset,boardY, boardX+(i*cellPixelSize)+offset,boardY+boardPixelSize);
            do Number.drawNumber(i, boardX + (i*cellPixelSize) - (cellPixelSize/2), boardY - 24);

            // draw horizontal lines
            do Screen.drawRectangle(boardX,boardY+(i*cellPixelSize)-offset, boardX+boardPixelSize,boardY+(i*cellPixelSize)+offset);
            do Number.drawNumber(i, boardX - 24, boardY + ((i-1)*cellPixelSize) + (cellPixelSize/2));

			let i = i + 1;
		}
        
        do Number.drawNumber(i, boardX + (i*cellPixelSize) - (cellPixelSize/2), boardY - 24);
        do Number.drawNumber(i, boardX - 24, boardY + ((i-1)*cellPixelSize) + (cellPixelSize/2));

		return;
	}

    method boolean drawMove(int row, int col, boolean isCross) {
		var int xl, yt;

        let row = row - 1;
        let col = col - 1;

        if(~isCellFree(row, col)) {
			return false;
		} 

		let xl = colToX(col) + ((cellPixelSize-signSize)/2);
		let yt = rowToY(row) + ((cellPixelSize-signSize)/2);
        if (isCross) {
            do drawCross(xl,yt);
        } else {
            do drawZero(xl,yt);
        }

		if (isCross) {
			let boardArray[row*boardSize+col] = 1;
		} else {
			let boardArray[row*boardSize+col] = 2;
		}

        let freeCellCount = freeCellCount - 1;

		return true;
	}

	method int rowToY(int r) {
		return boardY + (r*cellPixelSize);
	}
	
	method int colToX(int c) {
		return boardX + (c*cellPixelSize);
	}

	method void drawCross(int x, int y) {
		do Screen.drawLine(x,y+signSize,x+signSize,y);
		do Screen.drawLine(x,y+signSize,x+signSize,y);
			
		do Screen.drawLine(x,y,x+signSize,y+signSize);
		do Screen.drawLine(x,y,x+signSize,y+signSize);
	
		return;
	}

	method void drawZero(int x, int y) {
		var int rOut,rIn;
		let rOut = signSize/2;
		let rIn = rOut-lineThickness;
		
		do Screen.drawCircle(x+rOut,y+rOut,rOut);
		do Screen.setColor(false);
		do Screen.drawCircle(x+rOut,y+rOut,rIn);
		do Screen.setColor(true);
		return;
	}   

	method boolean isCellFree(int row, int col) {
		if (boardArray[row*boardSize+col] = 0) {
			return true;
		} 
		return false;
	}

	method boolean isFieldOccupied() {
		return freeCellCount=0;
	}

	method boolean isRowWin(int row) {
		var int i;
		var boolean isCrossFirst;
		let i = 0;
		let row = row - 1;
		let isCrossFirst = (boardArray[row*boardSize] = 1);

		while (i < boardSize) {
			if ((isCrossFirst & ~(boardArray[row * boardSize + i] = 1)) | 
				(~isCrossFirst & ~(boardArray[row * boardSize + i] = 2))) {
				return false;
			} 
			let i = i + 1;
		}
		return true;
	}

	method boolean isColumnWin(int column) {
		var int i;
		var boolean isCrossFirst;
		let i = 0;
		let column = column - 1;
		let isCrossFirst = (boardArray[column] = 1);

		while (i < boardSize) {
			if ((isCrossFirst & ~(boardArray[i * boardSize + column] = 1)) | 
				(~isCrossFirst & ~(boardArray[i * boardSize + column] = 2))) {
				return false;
			} 
			let i = i + 1;
		}

		return true;
	}

	method boolean isMainDiagonalWin(int row, int column) {
		var int i;
		var boolean isCrossFirst;
		let i = 0;
		let row = row - 1;
		let column = column - 1;
		let isCrossFirst = (boardArray[0] = 1);

		if (~(row = column)) {
			return false;
		}

		while (i < boardSize) {
			if ((isCrossFirst & ~(boardArray[i * boardSize + i] = 1)) | 
				(~isCrossFirst & ~(boardArray[i * boardSize + i] = 2))) {
				return false;
			} 
			let i = i + 1;
		}

		return true;
	}

	method boolean isAntiDiagonalWin(int row, int column) {
		var int i;
		var boolean isCrossFirst;
		let i = 0;
		let row = row - 1;
		let column = column - 1;
		let isCrossFirst = (boardArray[boardSize-1] = 1);

		if (~(row = boardSize-column-1)) {
			return false;
		}

		while (i < boardSize) {
			if ((isCrossFirst & ~(boardArray[i * boardSize+boardSize - i - 1] = 1)) |
				(~isCrossFirst & ~(boardArray[i * boardSize+boardSize - i - 1] = 2))) {
				return false;
			} 
			let i = i + 1;
		}

		return true;
	}

	method boolean isWin(int row, int column) {
		return isAntiDiagonalWin(row, column) | isMainDiagonalWin(row, column) | isColumnWin(column) | isRowWin(row);
	}

	method void drawWinRow(int row) {
		var int y, offset;
		let y = rowToY(row - 1) + (cellPixelSize / 2); 
		let offset = lineThickness / 2;
		do Screen.drawRectangle(boardX, y - offset, boardX + boardPixelSize, y + offset);
		return;
	}
	
	method void drawWinColumn(int column) {
		var int x, offset;
		let x = colToX(column - 1) + (cellPixelSize / 2); 
		let offset = lineThickness / 2;
		do Screen.drawRectangle(x - offset, boardY, x + offset, boardY + boardPixelSize);
		return;
	}

	method void drawWinMainDiagonal() {
		var int i, x, y;
		let x = boardX;
		let y = boardY;
		let i = lineThickness - 1;
		while(~(i < 0)) {
			do Screen.drawLine(x + i, y, x + boardPixelSize, y + boardPixelSize - i);
			do Screen.drawLine(x, y + i, x + boardPixelSize - i,y + boardPixelSize);
			let i = i - 1;
		}
		return;
	}

	method void drawWinAntiDiagonal() {
		var int i, x, y;
		let x = boardX;
		let y = boardY;
		let i = lineThickness - 1;
		while(~(i < 0)) {
			do Screen.drawLine(x, y + boardSize - i,x + boardSize - i, y);
			do Screen.drawLine(x + i, y + boardSize, x + boardSize, y + i);
			
			let i = i - 1;
		}
		return;
	}
}